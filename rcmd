#!/usr/bin/perl -s
use strict;
use warnings;
use lib "$ENV{HOME}/perl5/lib";
use Net::OpenSSH;

our ($h, $v);

my $usage = <<"EOF";
Execute command on hosts.txt over SSH.
usage: $0 [-hv] hosts.txt 'command'
  -h    print usage help
  -v    be more verbose
EOF

if ($h) {
    print $usage;
    exit;
}

@ARGV == 2 or die $usage;
my ( $hosts, $cmd ) = @ARGV;

my $contents = do { local ( @ARGV, $/ ) = $hosts; <> };
my @hosts = split ' ', $contents;

my %ssh;

# multiple connections are established in parallel:
for my $host (@hosts) {
    $ssh{$host} = Net::OpenSSH->new(
        $host,
        user    => 'root',
        async   => 1,
        timeout => 3,
        master_stderr_discard => 1,  # don't show the MOTD
    );
}

# then to run some command in all the hosts (sequentially):
for my $host (@hosts) {
    my $msg = "---> $host";
    $msg .= ": $cmd" if $v;
    print "$msg\n";
    $ssh{$host}->system($cmd);
}
